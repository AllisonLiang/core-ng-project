apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.framework'
    version = '7.0.3'
}

def elasticVersion = '7.3.1'
def kafkaVersion = '2.3.0'
def jacksonVersion = '2.9.9'
def junitVersion = '5.5.1'
def mockitoVersion = '3.0.0'
def assertjVersion = '3.13.2'

project(':core-ng-api') {
    apply from: file("${rootDir}/gradle/lib.gradle")
}

project(':core-ng') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-api")
        api 'org.slf4j:slf4j-api:1.8.0-beta4'
        implementation 'org.javassist:javassist:3.25.0-GA'
        implementation "com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        implementation 'com.squareup.okhttp3:okhttp:4.1.0'
        implementation 'io.undertow:undertow-core:2.0.26.Final'
        implementation "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        implementation 'org.xerial.snappy:snappy-java:1.1.7.3'      // used by kafka message compression
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeOnly 'org.hsqldb:hsqldb:2.4.1'
    }
}

project(':core-ng-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        api "org.mockito:mockito-core:${mockitoVersion}"
        api "org.assertj:assertj-core:${assertjVersion}"
        implementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        implementation "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        testRuntimeOnly 'org.hsqldb:hsqldb:2.4.1'
    }
}

project(':core-ng-mongo') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api 'org.mongodb:mongo-java-driver:3.10.2'
        testImplementation project(":core-ng-test")
    }
}

project(':core-ng-mongo-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-test")
        api project(":core-ng-mongo")
        implementation 'de.bwaldvogel:mongo-java-server:1.19.0'
    }
}

project(':core-ng-search') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api "org.elasticsearch.client:elasticsearch-rest-high-level-client:${elasticVersion}"
        api "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"                     // update jackson lib transitive dependencies from elasticsearch
        testImplementation project(":core-ng-test")
    }
}

project(':core-ng-search-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-test")
        api project(":core-ng-search")
        implementation "org.elasticsearch.plugin:transport-netty4-client:${elasticVersion}"
        implementation "org.elasticsearch.plugin:reindex-client:${elasticVersion}"
        implementation "org.codelibs.elasticsearch.module:lang-painless:${elasticVersion}"
        implementation "org.codelibs.elasticsearch.module:mapper-extras:${elasticVersion}"      // used by elasticsearch scaled_float
        implementation "org.codelibs.elasticsearch.module:analysis-common:${elasticVersion}"    // used by elasticsearch stemmer
        implementation 'org.apache.logging.log4j:log4j-core:2.11.1'                             // used by elasticsearch local node
    }
}

def mavenURL = hasProperty('mavenURL') ? mavenURL : null    // usage: "gradlew -PmavenURL=/path clean publish"

configure(subprojects.findAll { it.name.startsWith('core-ng') }) {
    apply plugin: 'maven-publish'
    if (mavenURL != null) {
        assert project.file(mavenURL).exists()
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.java
                    artifact sourcesJar
                }
            }
            repositories {
                maven { url mavenURL }
            }
        }
    }
}

project(':log-processor') {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation project(':core-ng')
        implementation project(':core-ng-search')
        testImplementation project(':core-ng-test')
        testImplementation project(':core-ng-search-test')
    }
}

project(':log-collector') {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation project(':core-ng')
        testImplementation project(':core-ng-test')
    }
}
